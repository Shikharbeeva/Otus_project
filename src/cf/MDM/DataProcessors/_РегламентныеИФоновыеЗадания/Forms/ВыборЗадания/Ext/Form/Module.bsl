
&НаКлиенте
Процедура ВыбратьЗадание(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаЗаданий.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОповеститьОВыборе(ТекущиеДанные[КлючТаблицы]);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаЗаданийЗначение = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыЗаданий);
	ТаблицаЗаданий.Загрузить(ТаблицаЗаданийЗначение);
	
	Если Параметры.ВариантОтбора = "ВидЗадания" Тогда
		
		КлючТаблицы = "ВидЗадания";
		Элементы.ТаблицаЗаданийВидЗадания.Видимость = Ложь;
		
	Иначе
		
		КлючТаблицы = "ИдентификаторЗадания";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ТекущееЗадание) Тогда
		
		ПараметрыОтбора = Новый Структура(КлючТаблицы, Параметры.ТекущееЗадание);
		НайденныеСтроки = ТаблицаЗаданий.НайтиСтроки(ПараметрыОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			ИдентификаторСтроки = НайденныеСтроки[0].ПолучитьИдентификатор();
			Элементы.ТаблицаЗаданий.ТекущаяСтрока = ИдентификаторСтроки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаданийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьЗадание(Неопределено);
	
КонецПроцедуры
