
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ТекущиеЗначения);
	
	Если ПортМенеджераКластера = 0 Тогда
		
		ПортМенеджераКластера = 1541;
		
	КонецЕсли;
	
	Если ПортАгентаСервера = 0 Тогда
		
		ПортАгентаСервера = 1540;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	Результат = Новый Структура("Событие", "НастройкаПараметровАдминистрирования");
	
	НовыеЗначения = Новый Структура("АдресАгентаСервера, ВыполнятьОперацииАдминистрированияНаСервере, ИмяАдминистратораИнформационнойБазы, ИмяАдминистратораКластера, ИмяИнформационнойБазы, ОбновлятьСостояниеИспользованияРегламентныхЗаданийПриЗапуске, ПарольАдминистратораИнформационнойБазы, ПарольАдминистратораКластера, ПортАгентаСервера, ПортМенеджераКластера, СохранятьПароли");
	ЗаполнитьЗначенияСвойств(НовыеЗначения, ЭтаФорма);
	Результат.Вставить("НовыеЗначения", НовыеЗначения);
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПортМенеджераКластераПоУмолчанию(Команда)
	
	ПортМенеджераКластера = 1541;
	
КонецПроцедуры

&НаКлиенте
Процедура ПортАгентаСервераПоУмолчанию(Команда)
	
	ПортАгентаСервера = 1540;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьПараметрыАдминистрирования(Команда)
	
	ОпределитьПараметрыАдминистрированияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьПараметрыАдминистрированияНаСервере()
	
	// Выделим части имени ИБ и имени сервера.
	СтрокаСоединенияИнформационнойБазы = СтрокаСоединенияИнформационнойБазы();
	Позиция = Найти(СтрокаСоединенияИнформационнойБазы, "Ref=""");
	СтрокаАдресАгентаСервера = Лев(СтрокаСоединенияИнформационнойБазы, Позиция - 3);
	СтрокаИмяИнформационнойБазы = Сред(СтрокаСоединенияИнформационнойБазы, Позиция + 5);
	
	// Параметры сервера.
	Позиция = Найти(СтрокаАдресАгентаСервера, """");
	СтрокаАдресАгентаСервера = Сред(СтрокаАдресАгентаСервера, Позиция + 1);
	Позиция = Найти(СтрокаАдресАгентаСервера, ":");
	
	Если Позиция = 0 Тогда
		
		АдресАгентаСервера = СтрокаАдресАгентаСервера;
		ПортМенеджераКластера = 1541;
		
	Иначе
		
		АдресАгентаСервера = Лев(СтрокаАдресАгентаСервера, Позиция - 1);
		ПортМенеджераКластера = Число(Сред(СтрокаАдресАгентаСервера, Позиция + 1));
		
	КонецЕсли;
	
	ПортАгентаСервера = ПортМенеджераКластера - 1;
	
	// Параметры ИБ.
	Позиция = Найти(СтрокаИмяИнформационнойБазы, """");
	ИмяИнформационнойБазы = Лев(СтрокаИмяИнформационнойБазы, Позиция - 1);
	
	// Определим текущего пользователя.
	ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	
	Если Не ТекущийПользователь.АутентификацияОС Тогда
		
		ИмяАдминистратораИнформационнойБазы = ТекущийПользователь.Имя;
		
	КонецЕсли;
	
КонецПроцедуры
