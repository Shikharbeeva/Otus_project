
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	
	Для Каждого Пользователь Из ПользователиИБ Цикл
		
		НоваяСтрока = ТаблицаПользователей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Пользователь);
		
	КонецЦикла;
	
	ТаблицаПользователей.Сортировать("Имя");
	
	Если ЗначениеЗаполнено(Параметры.ТекущийПользователь) Тогда
		
		ПараметрыОтбора = Новый Структура("Имя", Параметры.ТекущийПользователь);
		НайденныеСтроки = ТаблицаПользователей.НайтиСтроки(ПараметрыОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			ИдентификаторСтроки = НайденныеСтроки[0].ПолучитьИдентификатор();
			Элементы.ТаблицаПользователей.ТекущаяСтрока = ИдентификаторСтроки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПользователейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьПользователя(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПользователя(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаПользователей.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОповеститьОВыборе(ТекущиеДанные.Имя);
	
КонецПроцедуры
